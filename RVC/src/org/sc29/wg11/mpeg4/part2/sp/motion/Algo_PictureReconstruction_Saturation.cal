/*************************************************************************
* This software module was originally developed by David B. Parlour (Xilinx Inc.), 
* in the course of development of the ISO/IEC 23002-5 (Video Tool Library) 
* for reference purposes and its performance may not have been optimized. 
* This software module is an implementation of one or more tools as 
* specified by the ISO/IEC 23002-5.
* 
* ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
* license to copy, distribute, and make derivative works of this software 
* module or modifications thereof for use in implementations of the 
* ISO/IEC 23002-5 in products that satisfy conformance criteria (if any). 
* 
* Those intending to use this software module in products are advised that 
* its use may infringe existing patents. ISO/IEC have no liability for use 
* of this software module or modifications thereof.
* 
* Copyright is not released for products that do not conform to audiovisual 
* and image-coding related ITU Recommendations and/or ISO/IEC International 
* Standards. 
* 
* Assurance that the originally developed software module can be used 
* (1) in the ISO/IEC 23002-5 once the ISO/IEC 23002-5 has been adopted; and 
* (2) to develop the ISO/IEC 23002-5: 
* 
* Xilinx Inc.  grants ISO/IEC all rights necessary to 
* include the originally developed software module or modifications thereof 
* in the ISO/IEC 23002-5 and to permit ISO/IEC to offer You a royalty-free, 
* worldwide, non-exclusive, copyright license to copy, distribute, and make 
* derivative works for use in implementations of the ISO/IEC 23002-5 in 
* products that satisfy conformance criteria (if any), and to the extent 
* that such originally developed software module or portions of it are 
* included in the ISO/IEC 23002-5.  To the extent that Xilinx Inc. 
* owns patent rights that would be required to make, use, or 
* sell the originally developed software module or portions thereof 
* included in the ISO/IEC 23002-5 in a conforming product, Xilinx Inc.
* will assure the ISO/IEC that it is willing to 
* negotiate licenses under reasonable and non-discriminatory terms and 
* conditions with applicants throughout the world.
* 
* ISO/IEC gives You a free license to this software module or modifications 
* thereof for the sole  purpose of developing theISO/IEC 23002-5
* 
* Xilinx Inc. retains full right to modify and use the 
* code for its own purpose, assign or donate the code to a third party and 
* to inhibit third parties from using the code for products that do not 
* conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
* Standards. 
* 
* This copyright notice must be included in all copies or derivative works. 
* Copyright (c) ISO/IEC 2007-2011.
*************************************************************************/

/********************************
* Algo_PictureReconstruction_Saturation.cal
* 
* Author: David B. Parlour (dave.parlour@xilinx.com)
* Modified by: Christophe Lucarz (christophe.lucarz@epfl.ch)
*
* Add motion and texture to produce pixels
*****************************/
 

package org.sc29.wg11.mpeg4.part2.sp.motion;

import org.sc29.wg11.mpeg4.part2.Constants.*;
import org.sc29.wg11.mpeg4.part2.ConstantsBtype.*;
actor Algo_PictureReconstruction_Saturation () 
	int(size=PIX_SZ) MOT, 
	int(size=PIX_SZ) TEX, 
	bool ACCODED, 
	uint (size=4) BTYPE
		==> 
		int(size=PIX_SZ) VID 
:

	
	cmd.newvop: action BTYPE:[newvop]==> 
	guard
		newvop = NEWVOP
	end

	// Pure texture
	cmd.textureOnly: action ACCODED:[ac_coded], BTYPE:[btype]==> 
	guard
		btype =2
	end

	// Pure motion
	cmd.motionOnly: action  ACCODED:[ac_coded], BTYPE:[btype] ==> 
	guard
		not ac_coded
	end

	// Mixed texture and motion
	// (Also used to skip vop w,h)
	cmd.other: action ACCODED: [ac_coded],BTYPE:[btype]==>
	end
	
	texture: action TEX:[ tex ] repeat 64 ==> VID:[ tex ] repeat 64
	end

	motion: action  MOT:[ mot ] repeat 64 ==> VID:[ mot ] repeat 64
	end

	combine: action MOT:[ mot ] repeat 64, TEX:[ tex ] repeat 64 ==> VID:[ [ if tex[i] + mot[i] < 0 then
			0
		else
			if tex[i] + mot[i] > 255 then
				255
			else
				tex[i] + mot[i]
			end
		end : for int i in 0 .. 63 ] ] repeat 64
	end

	schedule fsm cmd :
		cmd ( cmd.newvop ) --> cmd;
		cmd ( cmd.textureOnly ) --> texture;
		cmd ( cmd.motionOnly ) --> motion;
		cmd ( cmd.other ) --> combine;
		texture ( texture ) --> cmd;
		motion ( motion ) --> cmd;
		combine ( combine ) --> cmd;
	end

	priority
		cmd.newvop > cmd.textureOnly > cmd.motionOnly > cmd.other;
	end

end