/*
 * Copyright (c) 2012, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/**
 * This actor is a picture buffer containing all reference pictures. It sends
 *		samples needed for the inter prediction
 * 
 * 
 * @input IsBiPred : Indicates when the current CU is using Bi-prediction.
 * 
 * @input IsReadOrNewSlc : Indicates if samples are asked from the current RPS
 *		or if there is a new picture.
 * 
 * @input Mv : movement vectors of the current CU.
 * 
 * @input PicSize : size of the current picture.
 * 
 * @input Poc : Picture Order Count of the current slice.
 * 
 * @output PocRef : POC used for the prediction of the current CU.
 * 
 * @input PredCuSize : Size of the largest side of the current CU.
 * 
 * @input Sample : Samples from the Inloop filter. It represents the pixels
 * 		which will be displayed.
 * 
 * 
 * @output Sample : Samples needed for the inter prediction.
 * 
 * 
 * @author Damien de Saint Jorre (EPFL) <damien.desaintjorre@epfl.ch>
 */
package devel.org.sc29.wg11.mpegh.part2.inter;

import std.util.Math.*;
import devel.org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;

actor DecodingPictureBuffer ()
	uint(size=2)  IsBiPredOrLx,
	uint(size=1)  IsReadOrNewSlc,
	int (size=32) Mv,
	uint(size=16) PicSize,
	uint(size=16) Poc,
	int (size=16) PocRef,
	uint(size=7)  PredCuSize,
	int (size=16) RpsPoc,
	uint(size=8)  Sample
		==>
	uint(size=8)  RefSample
		:

	/* /!\ : Check the size of the border. */
	uint(size=6)  BORDER_SIZE = 128;
	uint(size=8)  pictureBuffer[7][3][PICT_WIDTH+2*BORDER_SIZE][PICT_HEIGHT+2*BORDER_SIZE];
	int (size=16) pocRef[8];
	bool          isRef[7];
	bool          isStillRef[7];
	uint(size=7)  sideMax;

	int (size=4)  currIdx := -1;
	int (size=4)  lastIdx := -1;
	uint(size=6)  picSizeInCu[2];
	uint(size=16) picSize[2];
	uint(size=6)  xIdx;
	uint(size=6)  yIdx;
	uint(size=1)  numPocRef;
	uint(size=3)  nbList;
	uint(size=3)  sizeOfList;
	bool          isNewPicture;
	uint(size=8)  RefCu[(64+7)*(64+7)+(32+3)*(32+3)*2];

	/** Getting informations from new slice/picture. Updating these informations **/
	isNewSlice: action IsReadOrNewSlc:[isRead], PicSize:[picWidthInPix,
		picHeightInPix], Poc:[poc] ==>
	guard
		/* Fix me : We should know when there is a new picture */
		isRead = 0 and currIdx != -1 and poc = pocRef[currIdx&7]
	do
		isNewPicture := false;
		nbList := 0;
		isStillRef := [false: for int i in 0 .. 6];
	end
uint pictNum;
	isNewPict: action IsReadOrNewSlc:[isRead], PicSize:[picWidthInPix,
		picHeightInPix], Poc:[poc] ==>
	guard
		isRead = 0 and not(currIdx != -1 and poc = pocRef[currIdx&7])
	do
		println("Dpb ; Pict num "+pictNum);
		pictNum := pictNum + 1;
		picSizeInCu[0] := (picWidthInPix+63)/64;
		picSizeInCu[1] := (picHeightInPix+63)/64;
		picSize[0] := picWidthInPix;
		picSize[1] := picHeightInPix;
		xIdx := 0;
		yIdx := 0;
		lastIdx := currIdx;
		foreach uint i in 0 .. 6
		do
			if(not isRef[i]) then
				currIdx := i;
			end
		end
		isRef[currIdx]  := true;
		pocRef[currIdx] := poc;
		isNewPicture    := true;
		nbList          := 0;
		isStillRef := [false: for int i in 0 .. 6];
	end

	/** We get all the references from the 5 lists of the Rps
	 * (StCurrentBefore, StCurrentAfter, StFollowing, LtCurrent, LtFollowing)
	 **/
	getRps.launch: action RpsPoc:[numPic]==>
	guard
		nbList < 5
	do
		sizeOfList := numPic;
	end

	getRps.done: action ==>
	guard
		nbList = 5
	do
		/* We remove from the Dpb all the pictures who are not in the Rps of
		 * the current picture
		 */
		foreach int i in 0 .. 6
		do
			isRef[i] := isRef[i] and isStillRef[i];
		end
		isRef[currIdx] := true;
	end

	/** For each we check the index of the reference poc
	 * (in order to not remove them from the Dpb).
	 **/
	getRpsPoc.launch: action RpsPoc:[pocVal] ==>
	guard
		sizeOfList > 0
	do
		sizeOfList := sizeOfList - 1;
		foreach int i in 0 .. 6
		do
			if(pocRef[i] = pocVal)then
				isStillRef[i] := true;
			end
		end
	end

	getRpsPoc.done: action ==>
	guard
		sizeOfList = 0
	do
		nbList := nbList + 1;
	end

	/* We get the whole Cu decoded (luma and chroma components) and we save
	 * it in the Dpb (pictureBuffer).
	 */
	getCuPix.launch: action Sample:[samp] repeat 64*64 + 32*32*2==>
	guard
		yIdx < picSizeInCu[1]
	var
		uint(size=16) xPixIdx,
		uint(size=16) yPixIdx,
		uint tokenOffset
	do
		xPixIdx := BORDER_SIZE + xIdx*64;
		yPixIdx := BORDER_SIZE + yIdx*64;
		foreach uint y in 0 .. 63
		do
			foreach uint x in 0 .. 63
			do
				pictureBuffer[lastIdx][0][xPixIdx+x][yPixIdx+y] := samp[x+y*64];
			end
		end

		xPixIdx := (BORDER_SIZE + xIdx*64)>>1;
		yPixIdx := (BORDER_SIZE + yIdx*64)>>1;
		foreach uint component in 1 .. 2
		do
			tokenOffset:= 64*64 + 32*32*(component-1);
			foreach uint y in 0 .. 32
			do
				foreach uint x in 0 .. 32
				do
					pictureBuffer[lastIdx][component][xPixIdx+x][yPixIdx+y] :=
						samp[x+y*32 + tokenOffset];
				end
			end
		end

		xIdx := xIdx + 1;
		if(xIdx = picSizeInCu[0]) then
			xIdx := 0;
			yIdx := yIdx + 1;
		end
	end

	/* When we get the picture we add the border (in order to be able to give a
	 * reference pixels which can be outside of the picture).
	 */
	getCuPix.done: action ==>
	guard
		yIdx = picSizeInCu[1]
	var
		uint xSize := picSize[0],
		uint ySize := picSize[1]
	do
		/* Fill in luma border */
		foreach uint x in BORDER_SIZE .. xSize-1 + BORDER_SIZE
		do
			foreach uint y in 0 .. BORDER_SIZE - 1
			do
				pictureBuffer[lastIdx][0][x][y] :=
					pictureBuffer[lastIdx][0][x][BORDER_SIZE];
				pictureBuffer[lastIdx][0][x][y+ySize+BORDER_SIZE] :=
					pictureBuffer[lastIdx][0][x][ySize+BORDER_SIZE-1];
			end
		end
		foreach uint y in 0 .. ySize + 2*BORDER_SIZE
		do
			foreach uint x in 0 .. BORDER_SIZE - 1
			do
				pictureBuffer[lastIdx][0][x][y] :=
					pictureBuffer[lastIdx][0][BORDER_SIZE][y];
				pictureBuffer[lastIdx][0][x+xSize+BORDER_SIZE][y] :=
					pictureBuffer[lastIdx][0][xSize+BORDER_SIZE-1][y];
			end
		end

		/* Fill in chroma border */
		foreach uint comp in 1 .. 2
		do
			foreach uint x in BORDER_SIZE/2 .. xSize/2-1 + BORDER_SIZE/2
			do
				foreach uint y in 0 .. BORDER_SIZE/2 - 1
				do
					pictureBuffer[lastIdx][comp][x][y] :=
						pictureBuffer[lastIdx][comp][x][BORDER_SIZE/2];
					pictureBuffer[lastIdx][comp][x][y+ySize/2+BORDER_SIZE/2] :=
						pictureBuffer[lastIdx][comp][x][ySize/2+BORDER_SIZE/2-1];
				end
			end
			foreach uint y in 0 .. ySize/2 + BORDER_SIZE*2/2
			do
				foreach uint x in 0 .. BORDER_SIZE/2 - 1
				do
					pictureBuffer[lastIdx][comp][x][y] :=
						pictureBuffer[lastIdx][comp][BORDER_SIZE/2][y];
					pictureBuffer[lastIdx][comp][x+xSize/2+BORDER_SIZE/2][y] :=
						pictureBuffer[lastIdx][comp][xSize/2+BORDER_SIZE/2-1][y];
				end
			end
		end
	end

	getReadAddr: action IsReadOrNewSlc:[isRead], IsBiPredOrLx:[isBiPredOrLx] ==>
	guard
		isRead = 1
	do
		numPocRef :=
			if(isBiPredOrLx = BI_PRED) then
				2
			else
				1
			end;
	end

	/* We get all the informations needed for sending the reference Cu. We save
	 * these samples in RefCu, it will be sent with the actions sendCu.isYxY
	 */
	getMvInfo.launch: action PocRef:[poc], PredCuSize:[predCuSize],
		Mv:[mvX, mvY] ==>
	guard
		numPocRef != 0
	var
		uint(size=3) idx := 0,
		int (size=32) xOffset,
		int (size=32) yOffset
	do
		sideMax := predCuSize;
		foreach uint i in 0 .. 6
		do
			if(pocRef[i] = poc and isRef[i]) then
				idx := i;
			end
		end
		/* The mv components correspond to the absolute movement vector, that's
		 * why we don't need to add the address of the current Cu
		 */

		/* We remove 3 for each coordinate component, because for the luma
		 * prediction we need the 3 previous pixels
		 */
		xOffset := (mvX >> 2);
		xOffset := min(xOffset, picSize[0]+3);
		xOffset := max(xOffset, -(8 +3 -1) -(sideMax-1));
		xOffset := xOffset +BORDER_SIZE-3;
		yOffset := (mvY >> 2);
		yOffset := min(yOffset, picSize[1]+3);
		yOffset := max(yOffset, -(8 +3 -1) -(sideMax-1));
		yOffset := yOffset +BORDER_SIZE-3;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 7 to sideMax
		 */
		foreach int y in 0 .. sideMax+7 -1
		do
			foreach int x in 0 .. sideMax+7 -1
			do
				RefCu[x+y*(sideMax+7)] := pictureBuffer[idx][0][x+xOffset]
					[y+yOffset];
			end
		end

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xOffset := (mvX >> 3);
		xOffset := min(xOffset, picSize[0]/2+1);
		xOffset := max(xOffset, -(4 +1 -1) -(sideMax/2-1));
		xOffset := xOffset +BORDER_SIZE/2-1;
		yOffset := (mvY >> 3);
		yOffset := min(yOffset, picSize[1]/2+1);
		yOffset := max(yOffset, -(4 +1 -1) -(sideMax/2-1));
		yOffset := yOffset +BORDER_SIZE/2-1;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 3 to sideMax
		 */
		foreach int comp in 1 .. 2
		do
			foreach int y in 0 .. sideMax/2+3 -1
			do
				foreach int x in 0 .. sideMax/2+3 -1
				do
					RefCu[x+y*(sideMax/2+3) + (sideMax+7)*(sideMax+7) +
						(comp-1)*(sideMax/2+3)*(sideMax/2+3)]
						:= pictureBuffer[idx][comp][x+xOffset][y+yOffset];
				end
			end
		end
		numPocRef := numPocRef - 1;
	end

	/** We send the reference samples according to the size of the predCu. **/
	sendCu.is64x64: action ==> RefSample:[RefCu] repeat (64+7)*(64+7)+
		(32+3)*(32+3)*2
	guard
		sideMax = 64
	end

	sendCu.is32x32: action ==> RefSample:[RefCu] repeat (32+7)*(32+7)+
		(16+3)*(16+3)*2
	guard
		sideMax = 32
	end

	sendCu.is16x16: action ==> RefSample:[RefCu] repeat (16+7)*(16+7)+
		(8+3)*(8+3)*2
	guard
		sideMax = 16
	end

	sendCu.is8x8: action ==> RefSample:[RefCu] repeat (8+7)*(8+7)+(4+3)*(4+3)*2
	guard
		sideMax = 8
	end

	sendCu.is4x4: action ==> RefSample:[RefCu] repeat (4+7)*(4+7)+(2+3)*(2+3)*2
	guard
		sideMax = 4
	end

	getMvInfo.done: action ==>
	guard
		numPocRef = 0
	end

	checkIsNewPict.isTrue: action ==>
	guard
		isNewPicture
	end

	checkIsNewPict.isFalse: action ==>
	guard
		not isNewPicture
	end

	schedule fsm IsFirstNewPic:
		IsFirstNewPic (isNewPict )--> GetFirstRps;

		GetFirstRps (getRps.launch )--> GetRpsFirstPoc;
		GetFirstRps (getRps.done   )--> IsNewSlice;

		GetRpsFirstPoc (getRpsPoc.launch )--> GetRpsFirstPoc;
		GetRpsFirstPoc (getRpsPoc.done   )--> GetFirstRps;

		IsNewSlice (isNewPict   )--> GetRps;
		IsNewSlice (isNewSlice  )--> GetRps;
		IsNewSlice (getReadAddr )--> GetMvInfo;

		GetRps (getRps.launch )--> GetRpsPoc;
		GetRps (getRps.done   )--> CheckIsNewPict;

		GetRpsPoc (getRpsPoc.launch )--> GetRpsPoc;
		GetRpsPoc (getRpsPoc.done   )--> GetRps;

		CheckIsNewPict (checkIsNewPict.isTrue  )--> GetCuPix;
		CheckIsNewPict (checkIsNewPict.isFalse )--> IsNewSlice;

		GetCuPix (getCuPix.launch )--> GetCuPix;
		GetCuPix (getCuPix.done   )--> IsNewSlice;

		GetMvInfo (getMvInfo.launch )--> SendCu;
		GetMvInfo (getMvInfo.done   )--> IsNewSlice;
		SendCu    (sendCu           )--> GetMvInfo;
	end
end
