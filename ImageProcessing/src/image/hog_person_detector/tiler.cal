

/*
 * Copyright (c) 2017, Heriot-Watt University, Edinburgh
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the IRISA nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/*
 * Author: Mark Young & Teymoor Rasheed Ali my120@hw.ac.uk tr78@hw.ac.uk
 * Date: 27.02.2017
 */
package image.hog_person_detector;

actor tiler () uint(size=8) Gin ==> uint(size=8) Gout :

/* Constants */
	uint(size=8) imageHeight = 128;

	uint(size=8) windowDimension = 8;

	uint(size=8) imageWidth = 64;

	uint(size=16) bufferSize = imageWidth * imageHeight;

	/* Variables */
	uint(size=16) head := 0;

	uint(size=8) buffer [ bufferSize ];

	uint(size=8) y := 0;

	uint(size=8) x := 0;

	uint(size=8) xBase := 0;

	uint(size=8) yBase := 0;

	uint(size=8) out;

	read: action Gin:[ x ] ==>
	guard
		head < bufferSize
	do
		buffer[head] := x;
		head := head + 1;
	end

	transition: action ==>
	guard
		head = bufferSize
	do
		head := 0;
		x := 0;
		y := 0;
		xBase := 0;
	end

	write: action ==> Gout:[ out ]
	guard
		head < bufferSize
	do
		out := buffer[(y + yBase) * imageWidth + x + xBase];
		x := x + 1;
		if x >= windowDimension then
			x := 0;
			y := y + 1;
			if y >= windowDimension then
				xBase := xBase + windowDimension;
				y := 0;
				x := 0;
				if xBase >= imageWidth then
					yBase := yBase + windowDimension;
					xBase := 0;
				end
			end
		end
		head := head + 1;
	end

	reset: action ==>
	guard
		head >= bufferSize
	do
		head := 0;
	end

	schedule fsm read :
		read ( read ) --> read;
		read ( transition ) --> write;
		write ( write ) --> write;
		write ( reset ) --> read;
		end

end
