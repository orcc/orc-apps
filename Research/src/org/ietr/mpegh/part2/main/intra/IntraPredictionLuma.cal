/*
 * Copyright (c) 2012-2013, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/**
 * Decoding process for intra blocks.
 * 
 * 
 * @input CUInfo : Informations related to the current CU
 *        (size, address and mode : INTRA,INTER,...).
 * 
 * @input IntraPredMode : Luma and chroma intra prediction mode for the
 *        current CU.
 * 
 * @input PartMode : Giving the partitioning mode of the current CU.
 * 
 * @input PicSize : Size of the current picture.
 * 
 * @input Sample : Value of the YUV components of every pixel from the pictures.
 *        The tokens correspond to the pixels of the non filtered picture.
 * 
 * @input SplitTransf : split_transform_flag syntax element.
 * 
 * 
 * @output PredSample : Pixel's values predicted from the intra prediction
 *         process.
 * 
 * 
 * @author Damien de Saint Jorre (EPFL) <damien.desaintjorre@epfl.ch>
 */
 
package org.ietr.mpegh.part2.main.intra;

import std.util.Math.*;
import org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;
import org.sc29.wg11.mpegh.part2.main.intra.IntraFunctions.*;

actor IntraPredictionLuma(bool EnabSkipInPix)
	uint(size=16) CUInfo,
	uint(size=6)  IntraPredMode,
	uint(size=8)  LcuSizeMax,
    uint(size=8)  NeighbVal,
	uint(size=4)  PartMode,
	uint(size=16) PictSize,
	uint(size=16) SliceAddr,
	bool          SplitTransform,
	bool 		  StrongIntraSmoothing,
	uint(size=16) TilesCoord
		==>
	uint(size=8) PredSample
		:

    uint(size=16) NEIGHB_TAB_SZ   = 2*PICT_WIDTH;
	uint(size=16) NEIGHB_TAB_MASK = 2*PICT_WIDTH-1;

	uint(size=6) INTRA_PLANAR = 0;
	uint(size=6) INTRA_DC = 1;

    uint(size=1) ABOVE = 0;
	uint(size=1) LEFT  = 1;
	uint(size=8) leftBlkAvail[256] =
	[0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,

	 8, 1, 7, 1, 2, 1, 1, 1, 6, 1, 5, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,

	 0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,

	 8, 1, 7, 1, 2, 1, 1, 1, 6, 1, 5, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1];

	uint(size=8) aboveBlkAvail[256] =
	[0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,

	 0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,

	 16, 15, 2, 1, 14, 13, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 12, 11, 2, 1, 10,  9, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	  8,  7, 2, 1,  6,  5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	  4,  3, 2, 1,  2,  1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,

	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1];


	int(size=16) iLoop;

	uint(size=16) numTiles;
	uint(size=16) tilesBeg[MAX_NUM_TILES][2];
	uint(size=16) tilesEnd[MAX_NUM_TILES][2];

	int(size=16) puTilesBeg[2];
	int(size=16) puTilesEnd[2];

	uint(size=7)  nS;
	uint(size=3)  log2NS;
	uint(size=16) cuAddr[2];
	uint(size=5)  cuMode;
	uint(size=16) treeAddr[2];
	uint(size=16) pictSize[2];
	uint(size=16) sliceAddr[2];
	uint(size=3)  numPart;
	uint(size=3)  numPartDecoded;
    uint(size=7)  numPixNeighb;

	bool strongIntraSmoothing;
	bool constrIntraPred;
	bool isIndepSlc;
	uint(size=8) maxLcuSizeLuma;

	uint(size=6) predMode;
	uint(size=6) predModeTab;
    uint(size=9) trafoBlkIdx;
	uint(size=9) trafoSize;
    uint(size=9) trafoBlkSize;
	uint(size=9) nbBlkInPu;

    uint(size=8)  pNeighb_left[129];
	uint(size=8)  pNeighb_above[129];
	bool          puIsIntra[NEIGHB_TAB_SZ];
	uint(size=7)  neighbBlkSz;

	uint(size=8) lumaPred[64*64];
	uint(size=9) numBlkToSend;
	uint(size=9) idxBlk;

	function absCoordToRasterScan(uint(size=16) coord[2]) --> int
	var
		uint(size=4) xCoordInBlk = coord[0]/4,
		uint(size=4) yCoordInBlk = coord[1]/4
	:
		if((xCoordInBlk & 1)!=0) then 1   else 0 end +
		if((xCoordInBlk & 2)!=0) then 4   else 0 end +
		if((xCoordInBlk & 4)!=0) then 16  else 0 end +
		if((xCoordInBlk & 8)!=0) then 64  else 0 end +
		if((yCoordInBlk & 1)!=0) then 2   else 0 end +
		if((yCoordInBlk & 2)!=0) then 8   else 0 end +
		if((yCoordInBlk & 4)!=0) then 32  else 0 end +
		if((yCoordInBlk & 8)!=0) then 128 else 0 end
	end

	procedure getNeighbSamples(uint(size=16) cuAddr[2],
		uint(size=7) nS, uint(size=3) log2NS, uint(size=8) neighbours[130 + 2 * 66], uint(size=16) neighbOffset)
	var
		int minVal[2], int maxVal[2],
		int xMin, int yMin, int xMax, int yMax,
		int xIdx, int yIdx,
		int origPixIdx,
		uint(size=8) pixVal
	begin
		neighbExist(cuAddr, minVal, maxVal, nS);
		xMin := minVal[0];
		yMin := minVal[1];
		xMax := maxVal[0];
		yMax := maxVal[1];

		origPixIdx := pictSize[1] - cuAddr[1] + cuAddr[0];
		foreach int y in 0 .. 2 * nS
		do
			pNeighb_left[y] := neighbours[neighbOffset + 2 * nS - y];
		end
	
		foreach int x in 0 .. 2 * nS
		do
			pNeighb_above[x] := neighbours[neighbOffset + 2 * nS + x];
		end
		if(constrIntraPred) then
			if(yMax = 0 || not puIsIntra[(origPixIdx-1-(yMax-1))&NEIGHB_TAB_MASK]) then
				pixVal := 128;
	
				xIdx := xMax-1;
				while(xIdx >= xMin)
				do
					if(puIsIntra[(origPixIdx+1+xIdx-1)&NEIGHB_TAB_MASK]) then
						pixVal := pNeighb_above[xIdx];
					end
					xIdx := xIdx - 1;
				end
	
				if(puIsIntra[origPixIdx & NEIGHB_TAB_MASK]) then
					pixVal := pNeighb_left[0];
				end
				foreach int y in yMin .. yMax-2
				do
					if(puIsIntra[(origPixIdx-1-y)&NEIGHB_TAB_MASK]) then
						pNeighb_left[y+1] := pNeighb_left[y+2];
					end
				end
				pNeighb_left[yMax-1+1] := pixVal;
			end
	
			yIdx := yMax - 2;
			while(yIdx >= yMin)
			do
				if(not puIsIntra[(origPixIdx-1-yIdx)&NEIGHB_TAB_MASK]) then
					pNeighb_left[yIdx+1] := pNeighb_left[yIdx+2];
				end
				yIdx := yIdx - 1;
			end
	
			if(not puIsIntra[origPixIdx & NEIGHB_TAB_MASK])then
				pNeighb_left[0]  := pNeighb_left[1];
				pNeighb_above[0] := pNeighb_left[1];
			end
			foreach int x in xMin .. xMax - 1
			do
				if(not puIsIntra[(origPixIdx+1+x)&NEIGHB_TAB_MASK]) then
					pNeighb_above[x+1] := pNeighb_above[x];
				end
			end
		end

		if(yMax != yMin) then
			foreach int y in 0 .. yMin-1
			do
				pNeighb_left[y+1] := pNeighb_left[yMin+1];
			end
			foreach int y in yMax .. 2*nS-1
			do
				pNeighb_left[y+1] := pNeighb_left[yMax-1+1];
			end
		end

		if(xMax != xMin) then
			foreach int x in 0 .. xMin-1
			do
				pNeighb_above[x] := pNeighb_left[0+1];
			end
			foreach int x in xMax .. 2*nS
			do
				pNeighb_above[x] := pNeighb_above[xMax-1];
			end
		end

		/* When the neighbours are not available, the nearest valid value is
		 * propagated to the other neighbours
		 */
		if(xMax=xMin && yMax=yMin) then
			foreach int idx in 0 .. 2*nS
			do
				pNeighb_above[idx] := 128;
				pNeighb_left[idx]  := 128;
			end
		else if(xMax=xMin && yMax!=yMin) then
			foreach int x in 0 .. 2*nS
			do
				pNeighb_above[x] := pNeighb_left[0+1];
			end
		else if(xMax!=xMin && yMax=yMin) then
			foreach int y in 0 .. 2*nS-1
			do
				pNeighb_left[y+1] := pNeighb_above[0+1];
			end
			pNeighb_above[0] := pNeighb_above[0+1];
		else if(xMin!=0 || yMin != 0) then
			pNeighb_above[0] := pNeighb_left[0+1];
		end end end end
		
		filterNeighbSamp(nS, log2NS);
        pNeighb_left[0] := pNeighb_above[0];
	end

	/* Check if the neighbors are available. maxVal contained either 2*nS or
	 * the index of the first pixel unavailable.
	 */
	procedure neighbExist(uint(size=16) cuAddr[2], int minVal[2], int maxVal[2],
		uint(size=7) nS)
	var
		uint(size=7) maxCuSize = maxLcuSizeLuma,
		int xMin := 0, int yMin := 0, int xMax := 0, int yMax := 0,
		int numPixRemain
	begin
		if(cuAddr[0]!=0) then
			numPixRemain :=
				if((cuAddr[0]&~(maxCuSize-1)) =
					((cuAddr[0]-1)&~(maxCuSize-1))) then
					leftBlkAvail[absCoordToRasterScan(cuAddr)]<<2
				else
					maxCuSize-(cuAddr[1]&(maxCuSize-1))
				end;
			yMax := min(min(2*nS, numPixRemain), pictSize[1]-cuAddr[1]);
			if(isIndepSlc) then
				if(cuAddr[0] <= sliceAddr[0]) then
					if(sliceAddr[1]+maxCuSize>cuAddr[1]+2*nS)then
						yMin := yMax;
					else if(sliceAddr[1]+maxCuSize>cuAddr[1]) then
						yMin := sliceAddr[1] + maxCuSize - cuAddr[1];
					end end
				else
					if(sliceAddr[1]>cuAddr[1]+2*nS) then
						yMin := yMax;
					else if(sliceAddr[1]>cuAddr[1]) then
						yMin := sliceAddr[1] - cuAddr[1];
					end end
				end
			end
			if(puTilesBeg[0] > cuAddr[0]-1) then
				yMin := yMax;
			else
				if(puTilesBeg[1] - cuAddr[1] > yMin) then
					yMin := max(puTilesBeg[1] - cuAddr[1] + 1, yMin);
				end
				if(puTilesEnd[1] - cuAddr[1] < yMax) then
					yMax := min(puTilesEnd[1] - cuAddr[1] + 1, yMax);
				end
			end
		end

		if(cuAddr[1]!=0) then
			numPixRemain :=
				if((cuAddr[1]&~(maxCuSize-1)) =
					((cuAddr[1]-1)&~(maxCuSize-1))) then
					min( aboveBlkAvail[absCoordToRasterScan(cuAddr)]<<2,
						pictSize[0]-cuAddr[0])
				else
					pictSize[0]-cuAddr[0]
				end;
			xMax := min(2*nS, numPixRemain)+1;
			if(isIndepSlc) then
				if(cuAddr[1] = sliceAddr[1]) then
					xMin := xMax;
				else if((cuAddr[1] = sliceAddr[1]+maxCuSize) && (cuAddr[0]-1<sliceAddr[0])) then
					if(sliceAddr[0]>cuAddr[0]+2*nS) then
						xMin := xMax;
					else if(sliceAddr[0]>cuAddr[0]-1) then
						xMin := sliceAddr[0]-cuAddr[0]+1;
					end end
				end end
			end
			if(puTilesBeg[1] > cuAddr[1]-1) then
				xMin := xMax;
			else
				if(puTilesBeg[0] - cuAddr[0] > xMin) then
					xMin := max(puTilesBeg[0] - cuAddr[0] + 1, xMin);
				end
				if(puTilesEnd[0] - cuAddr[0] < xMax) then
					xMax := min(puTilesEnd[0] - cuAddr[0] + 1, xMax);
				end
			end
		end

		maxVal[0] := xMax;
		maxVal[1] := yMax;
		minVal[0] := xMin;
		minVal[1] := yMin;
	end

	/* Filtering process of neighbouring samples. */
	procedure filterNeighbSamp(uint(size=7) nS, uint(size=3) log2NS)
	var
		uint bitDepthY = 8,
		uint threshold = 1<<(bitDepthY-5),
		uint(size=8) bottomLeft = pNeighb_left[2*nS],
		uint(size=8) topLeft = pNeighb_above[0],
		uint(size=8) topRight = pNeighb_above[2*nS],
		uint(size=8) edgePix[2] = [topRight, bottomLeft],
		bool bilinearLeft,
		bool bilinearAbove,
		uint(size=5) minDistVertHor,
		uint(size=8) pFNeighb[2][64]
	begin
		if(predMode != INTRA_DC) then
			minDistVertHor := min(abs(predMode-26), abs(predMode-10));
			if(nS=4  && minDistVertHor>10 ||
			   nS=8  && minDistVertHor>7 ||
			   nS=16 && minDistVertHor>1 ||
			   nS=32 && minDistVertHor>0 ||
			   nS=64 && minDistVertHor>10) then
				if(strongIntraSmoothing) then
					bilinearLeft  := abs(bottomLeft+topLeft-2*pNeighb_left[nS]) < threshold;
					bilinearAbove := abs(topLeft+topRight-2*pNeighb_above[nS]) < threshold;

					if(nS>=32 && bilinearLeft && bilinearAbove) then
						foreach uint neighb in 0 .. 1
						do
							foreach uint i in 1 .. nS*2-1
							do
								pFNeighb[neighb][i] := ((2*nS-i)*topLeft+ i*edgePix[neighb]+nS)>>log2NS+1;
							end
						end
						
						foreach uint i in 1 .. nS*2-1
						do
							pNeighb_above[i] := pFNeighb[ABOVE][i];
							pNeighb_left [i] := pFNeighb[LEFT][i];
						end
						
					else
						
						pFNeighb[ABOVE][0] := (pNeighb_above[0] + 2*pNeighb_above[0+1] + pNeighb_above[1+1] + 2) >> 2;
						pFNeighb[LEFT][0] := (pNeighb_above[0] + 2*pNeighb_left[0+1] + pNeighb_left[1+1] + 2) >> 2;
						foreach uint i in 1 .. nS*2-2
						do
							pFNeighb[ABOVE][i] := (pNeighb_above[i-1+1] + 2*pNeighb_above[i+1] + pNeighb_above[i+1+1] + 2) >> 2;
							pFNeighb[LEFT][i] := (pNeighb_left[i-1+1] + 2*pNeighb_left[i+1] + pNeighb_left[i+1+1] + 2) >> 2;
						end
							
						pNeighb_above[0] := (pNeighb_left[0+1] + 2*pNeighb_above[0] + pNeighb_above[0+1] + 2) >> 2;
						
						foreach uint i in 0 .. nS*2-2
						do
							pNeighb_above[i+1] := pFNeighb[ABOVE][i];
							pNeighb_left [i+1] := pFNeighb[LEFT][i];
						end
						
	 				end
				else
					
					pFNeighb[ABOVE][0] := (pNeighb_above[0] + 2*pNeighb_above[0+1] + pNeighb_above[1+1] + 2) >> 2;
					pFNeighb[LEFT][0] := (pNeighb_above[0] + 2*pNeighb_left[0+1] + pNeighb_left[1+1] + 2) >> 2;
					foreach uint i in 1 .. nS*2-2
					do
						pFNeighb[ABOVE][i] := (pNeighb_above[i-1+1] + 2*pNeighb_above[i+1] + pNeighb_above[i+1+1] + 2) >> 2;
						pFNeighb[LEFT][i] := (pNeighb_left[i-1+1] + 2*pNeighb_left[i+1] + pNeighb_left[i+1+1] + 2) >> 2;
					end
					
					pNeighb_above[0] := (pNeighb_left[0+1] + 2*pNeighb_above[0] + pNeighb_above[0+1] + 2) >> 2;
					
					foreach uint i in 0 .. nS*2-2
					do
						pNeighb_above[i+1] := pFNeighb[ABOVE][i];
						pNeighb_left [i+1] := pFNeighb[LEFT][i];
					end
					
	 			end
	 		end
		end
	end

	/* Implementation of Intra_DC prediction mode. */
	procedure intraDcPred(uint(size=7) nS, uint(size=3) log2NS)
	var
		uint(size=1) minVal,
		uint(size=13) dcVal
	begin
		dcVal := nS;
		foreach uint x in 0 .. nS-1
		do
			dcVal := dcVal + pNeighb_above[x+1];
		end
		foreach uint y in 0 .. nS-1
		do
			dcVal := dcVal + pNeighb_left[y+1];
		end
		dcVal := dcVal >> (log2NS+1);

		
		minVal := if(nS>16) then 0 else 1 end;
		if(nS<=16) then
			lumaPred[0] := (pNeighb_left[0+1] + 2*dcVal +
				pNeighb_above[0+1] + 2)>>2;
			foreach uint x in 1 .. nS-1
			do
				lumaPred[x] := (pNeighb_above[x+1] + 3*dcVal + 2)>>2;
			end
			foreach uint y in 1 .. nS-1
			do
				lumaPred[y * 64] := (pNeighb_left[y+1] + 3*dcVal + 2)>>2;
			end
		end
		foreach uint y in minVal .. nS-1
		do
			foreach uint x in minVal .. nS-1
			do
				lumaPred[x + y * 64] := dcVal;
			end
		end
		
	end

    procedure intraPrediction(uint(size=8) neighbours[130 + 2 * 66])
	var
		uint(size=7)  treeNS,
		uint(size=3)  treeLog2NS,
		uint(size=16) locAddr[2],
		uint(size=16) neighbOffset := 0
	begin
		/* Computing the size and the address of the current block. */
		treeNS := trafoBlkSize * 4;
		treeLog2NS := log2_tab[treeNS];
		zScanToAbsCoord(trafoBlkIdx, treeAddr);
		treeAddr := [treeAddr[i] + cuAddr[i]: for int i in 0 .. 1];
		locAddr := [treeAddr[i]:for int i in 0 .. 1];
		neighbOffset := 0;

		getNeighbSamples(locAddr, treeNS, treeLog2NS, neighbours, neighbOffset);

		if(predMode = INTRA_PLANAR) then
			intraPlanarPred(lumaPred, pNeighb_above, pNeighb_left, 64, treeLog2NS);
		else if(predMode = INTRA_DC) then
			intraDcPred(treeNS, treeLog2NS);
		else
			intraAngularPred(lumaPred, pNeighb_above, pNeighb_left, 64, 0, predMode, treeLog2NS);
		end end
		neighbOffset := neighbOffset + treeNS * 4 + 2;
	end

	/* Updating the picture size. */
	isNewSliceOrPict.notDep: action PartMode:[partMode], LcuSizeMax:[log2_lcuSz],
		PictSize:[pictSizeVal] repeat 2, SliceAddr:[slcAddr] repeat 2,
		StrongIntraSmoothing:[smoothing, constrIntra],
		TilesCoord:[valNumTiles] ==>
	guard
		partMode = PART_MODE_SLICE_INDEP ||
		partMode = PART_MODE_PICT
	do
//		println("countReadIntra = " + countRead);
//		println("countWriteIntra = " + countWrite);
		// countRead := 0;
		countWrite := 0;
		sliceAddr := [slcAddr[i]:for int i in 0 .. 1];
		pictSize := [pictSizeVal[i]:for int i in 0 .. 1];
		strongIntraSmoothing := smoothing;
		constrIntraPred := constrIntra;
		isIndepSlc := partMode = PART_MODE_SLICE_INDEP;
		numTiles := valNumTiles;
		iLoop := 0;
		maxLcuSizeLuma := 1 << log2_lcuSz;
	end

	/* Updating the picture size. */
	isNewSliceOrPict.dep: action PartMode:[partMode], SliceAddr:[slcAddr] repeat 2,
		StrongIntraSmoothing:[smoothing, constrIntra]==>
	guard
		partMode = PART_MODE_SLICE_DEP
	do
		sliceAddr := [slcAddr[i]:for int i in 0 .. 1];
		strongIntraSmoothing := smoothing;
		constrIntraPred := constrIntra;
		isIndepSlc := partMode = PART_MODE_SLICE_INDEP;
	end

	getTilesCoord.launch: action TilesCoord:[tilesCoord] repeat 4 ==>
	guard
		iLoop < numTiles
	do
		tilesBeg[iLoop] := [tilesCoord[0], tilesCoord[1]];
		tilesEnd[iLoop] := [tilesCoord[2], tilesCoord[3]];
		iLoop := iLoop + 1;
	end

	getTilesCoord.done: action ==>
	guard
		iLoop = numTiles
	end

	/* Check when there are two parts in the CU.  */
	getPartMode.isNonQuadTree: action PartMode:[partMode] ==>
	guard
		partMode != PART_MODE_SLICE_DEP && partMode != PART_MODE_SLICE_INDEP &&
		partMode != PART_MODE_PICT,
		partMode != PART_2Nx2N && partMode != PART_NxN
	end

	/* Check if all parts of the CU are a square. */
	getPartMode.isQuadTree: action PartMode:[partMode] ==>
	guard
		partMode = PART_2Nx2N || partMode = PART_NxN
	do
		numPart := partModeToNumPart[partMode];
		numPartDecoded := 0;
	end

	/* Get the information concerning the CU to be able to save the values in
	 * currPict.
	 */
	getCuInfo.launch: action CUInfo:[cuInfo] repeat 5 ==>
	guard
		numPartDecoded < numPart,
		not EnabSkipInPix || cuInfo[0] != IN_SAMP
	do
		cuMode := cuInfo[0];
		if(EnabSkipInPix && (cuMode & NO_IN_SAMP) != 0) then
			cuMode := cuMode - NO_IN_SAMP;
		end
		nS := cuInfo[3];
		cuAddr := [cuInfo[1], cuInfo[2]];
		numPartDecoded := numPartDecoded + 1;
	end
	/* When there are two parts, we get both part in once in order to perform a
	 * raster scan on the square (the SelectCU actor reorder the different
	 * blocks)
	 */
	getCuInfo.nonQT.isInter: action CUInfo:[cuInfo] repeat 10 ==>
	do
		cuMode := cuInfo[0];
		if(EnabSkipInPix && (cuMode & NO_IN_SAMP) != 0) then
			cuMode := cuMode - NO_IN_SAMP;
		end
		nS := max(cuInfo[3], cuInfo[4]);
		idxBlk := 0;
		neighbBlkSz  := nS;
        numBlkToSend := (nS*nS)/16;
		treeAddr := [cuInfo[1], cuInfo[2]];
		numPart := 2;
		numPartDecoded := 2;
        trafoBlkIdx := 0;
		trafoBlkSize := nS / 4;
		trafoSize := nS * nS / 16;
		nbBlkInPu := nS * nS / 16;
		numPixNeighb := (nS * 2) - 1;
	end

	getCuInfo.done: action ==>
	guard
		numPartDecoded = numPart
	end

	/* When it's an intra CU, getting the intra prediction mode for chroma and luma component. */
	getCuMode.isIntra: action IntraPredMode:[predMode] repeat 2 ==>
	guard
		cuMode = INTRA
	do
		foreach int tileIdx in 0 .. numTiles - 1
		do
			if(cuAddr[0] >= tilesBeg[tileIdx][0] && cuAddr[0] < tilesEnd[tileIdx][0] &&
			   cuAddr[1] >= tilesBeg[tileIdx][1] && cuAddr[1] < tilesEnd[tileIdx][1]) then
			   	puTilesBeg := [tilesBeg[tileIdx][0], tilesBeg[tileIdx][1]];
			   	puTilesEnd := [tilesEnd[tileIdx][0], tilesEnd[tileIdx][1]];
			 end
		end

		predModeTab := predMode[0];
		log2NS := 2;
		while((1<<log2NS) < nS)
		do
			log2NS := log2NS+1;
		end
        trafoBlkIdx := 0;
		trafoBlkSize := nS / 4;
		trafoSize := nS * nS / 16;
		nbBlkInPu := nS * nS / 16;
	end

	/* Check if it's an inter prediction, then the pixels sent will be saved in
	 * currPict.
	 */
	getCuMode.isInter: action ==>
	guard
		cuMode = INTER || cuMode = SKIP
	do
		idxBlk := 0;
		numBlkToSend := (nS*nS)/16;
		neighbBlkSz  := nS;
        treeAddr := [cuAddr[i]: for int i in 0 .. 1];
		numPixNeighb := nS * 2 - 1;
		trafoBlkIdx := 0;
		trafoBlkSize := nS / 4;
		trafoSize := nS * nS / 16;
		nbBlkInPu := nS * nS / 16;
	end

	/* When it's neither an intra nor inter prediction CU, do neither
	 * prediction nor saving pixels' value
	 */
	getCuMode.isOther: action ==>
	guard
		cuMode = OTHER
	end

	/* Check if the intra prediction CU has to be split in 4 "subCU" */
	getSplitTrafo.isTrue: action SplitTransform:[splitTrafo] ==>
	guard
		splitTrafo && trafoBlkIdx < nbBlkInPu
	do
		trafoBlkSize := trafoBlkSize >> 1;
		trafoSize := trafoSize >> 2;
	end

	/* Check if the intra prediction will not be split again, then realize the
	 * prediction for this part of CU
	 */
	getSplitTrafo.isFalse: action SplitTransform:[splitTrafo] ==>
	guard
        not splitTrafo && trafoBlkIdx < nbBlkInPu
		
	do
		neighbBlkSz  := trafoBlkSize * 4;
        numBlkToSend := neighbBlkSz*neighbBlkSz/16;
		numPixNeighb := neighbBlkSz * 2 - 1;
		idxBlk := 0;
        predMode := predModeTab;
    end


	getSplitTrafo.isInter: action SplitTransform:[splitTrafo] ==>
	guard
		trafoBlkIdx < nbBlkInPu
	do
		if(splitTrafo) then
			trafoSize := trafoSize >> 2;
			trafoBlkSize := trafoBlkSize >> 1;
		else
			trafoBlkIdx := trafoBlkIdx + trafoSize;
			trafoBlkSize :=1 << (castBoolToInt((trafoBlkIdx & 3) = 0) +
				castBoolToInt((trafoBlkIdx & 15) = 0) +
				castBoolToInt((trafoBlkIdx & 63) = 0));
			trafoSize := trafoBlkSize * trafoBlkSize;
		end
	end

	getSplitTrafo.done: action ==>
	guard
		trafoBlkIdx = nbBlkInPu
	var
		uint tabOff
	do
		if(cuMode != INTRA && constrIntraPred) then
			tabOff := pictSize[1] - (treeAddr[1] + neighbBlkSz - 1) + treeAddr[0];
			foreach int i in 0 .. numPixNeighb - 1
			do
				puIsIntra[tabOff + i] := false;
	        end
		end
	end
	
	int countWrite := 0;
	
	/* Sending one 4x4 luma block. */
	sendSamples.launch: action ==> PredSample:[sampleVal] repeat 16
	guard
		idxBlk < numBlkToSend
	var
		uint(size=8) sampleVal[16],
		uint(size=7) idxSample := 0,
		uint(size=16) blkAddr[2]
	do
		countWrite := countWrite + 16;
		zScanToAbsCoord(idxBlk, blkAddr);

		foreach uint y in blkAddr[1] .. blkAddr[1]+3
		do
			foreach uint x in blkAddr[0] .. blkAddr[0]+3
			do
				sampleVal[idxSample] := lumaPred[x + y * 64];
				idxSample := idxSample + 1;
			end
		end
		
		idxBlk := idxBlk + 1;
	end

	sendSamples.done: action ==>
	guard
		idxBlk = numBlkToSend
	var
		uint tabOff
	do
		/* going to the next "branch of the tree" */
		trafoBlkIdx := trafoBlkIdx + trafoSize;
		trafoBlkSize :=1 << (castBoolToInt((trafoBlkIdx & 3) = 0) +
			castBoolToInt((trafoBlkIdx & 15) = 0) +
			castBoolToInt((trafoBlkIdx & 63) = 0));
		trafoSize := trafoBlkSize * trafoBlkSize;

		if(constrIntraPred) then
			tabOff := pictSize[1] - (treeAddr[1] + neighbBlkSz - 1) + treeAddr[0];
			foreach int i in 0 .. numPixNeighb - 1
			do
				puIsIntra[tabOff + i] := true;
			end
		end

		idxBlk := 0;
	end
	
	computeIntraPu.is4x4: action NeighbVal:[neighbVal] repeat 18 ==>
	guard
		neighbBlkSz = 4
	do
		intraPrediction(neighbVal);
	end

	computeIntraPu.is8x8: action NeighbVal:[neighbVal] repeat 34 ==>
	guard
		neighbBlkSz = 8
	do
		intraPrediction(neighbVal);
	end

	computeIntraPu.is16x16: action NeighbVal:[neighbVal] repeat 66 ==>
	guard
		neighbBlkSz = 16
	do
		intraPrediction(neighbVal);
	end

	computeIntraPu.is32x32: action NeighbVal:[neighbVal] repeat 130 ==>
	guard
		neighbBlkSz = 32
	do
		intraPrediction(neighbVal);
	end

	schedule fsm GetPartMode:
		GetPartMode(isNewSliceOrPict.notDep   )--> GetTilesCoord;
		GetPartMode(isNewSliceOrPict.dep      )--> GetPartMode;
		GetPartMode(getPartMode.isQuadTree    )--> GetCuInfo;
		GetPartMode(getPartMode.isNonQuadTree )--> GetCuInfoNonQT;

		GetTilesCoord (getTilesCoord.launch )--> GetTilesCoord;
		GetTilesCoord (getTilesCoord.done   )--> GetPartMode;

		GetCuInfoNonQT (getCuInfo.nonQT )--> GetSplitTrafoInter;

		GetCuInfo (getCuInfo.launch    )--> GetCuMode;
		GetCuInfo (getCuInfo.done      )--> GetPartMode;

		GetCuMode (getCuMode.isIntra )--> GetSplitTrafoIntra;
		GetCuMode (getCuMode.isInter )--> GetSplitTrafoInter;
		GetCuMode (getCuMode.isOther )--> GetCuInfo;

		GetSplitTrafoIntra (getSplitTrafo.isTrue  )--> GetSplitTrafoIntra;
		GetSplitTrafoIntra (getSplitTrafo.isFalse )--> ComputeIntraPu;
		GetSplitTrafoIntra (getSplitTrafo.done    )--> GetCuInfo;

        ComputeIntraPu (computeIntraPu )--> SendSamples;

		SendSamples (sendSamples.launch )--> SendSamples;
		SendSamples (sendSamples.done   )--> GetSplitTrafoIntra;

        GetSplitTrafoInter (getSplitTrafo.isInter  )--> GetSplitTrafoInter;
		GetSplitTrafoInter (getSplitTrafo.done     )--> GetCuInfo;
	end
end