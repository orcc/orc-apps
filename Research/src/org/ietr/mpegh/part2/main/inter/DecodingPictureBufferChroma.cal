/*
 * Copyright (c) 2012, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/**
 * This actor is a picture buffer containing all reference pictures. It sends
 *		samples needed for the inter prediction
 * 
 * 
 * @input IsBiPred : Indicates when the current CU is using Bi-prediction.
 * 
 * @input IsReadOrNewSlc : Indicates if samples are asked from the current RPS
 *		or if there is a new picture.
 * 
 * @input Mv : movement vectors of the current CU.
 * 
 * @input PicSize : size of the current picture.
 * 
 * @input Poc : Picture Order Count of the current slice.
 * 
 * @output PocRef : POC used for the prediction of the current CU.
 * 
 * @input PredCuSize : Size of the largest side of the current CU.
 * 
 * @input Sample : Samples from the Inloop filter. It represents the pixels
 * 		which will be displayed.
 * 
 * 
 * @output Sample : Samples needed for the inter prediction.
 * 
 * 
 * @author Damien de Saint Jorre (EPFL) <damien.desaintjorre@epfl.ch>
 */
package org.ietr.mpegh.part2.main.inter;

import std.util.Math.*;
import devel.org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;

// DR 1402
import org.ietr.mpegh.part2.main.inter.DecodingPictureBufferFunctions.*;

actor DecodingPictureBufferChroma ()
	uint(size=2)  IsBiPredOrLx,
	uint(size=1)  IsReadOrNewSlc,
	int (size=32) Mv,
	uint(size=16) PicSize,
	int(size=16) Poc,
	int (size=16) PocRef,
	uint(size=7)  PredCuSize,
	int (size=16) RpsPoc,
	uint(size=8)  Sample
		==>
	uint(size=8)  RefSample
		:


	/* /!\ : Check the size of the border. */
	uint(size = 16) BORDER_SIZE = 128;
	uint(size = 8) DPB_SIZE = 17;

	uint(size=8)  pictureBuffer[DPB_SIZE][PICT_HEIGHT/2+2*BORDER_SIZE][PICT_WIDTH/2+2*BORDER_SIZE];
	int (size=16) pocRef[DPB_SIZE];
	bool          isRef[DPB_SIZE];
	bool          isStillRef[DPB_SIZE];
	

	int (size=4)  currIdx := -1;
	int (size=4)  lastIdx := -1;
	uint(size=6)  picSizeInCu[2];
	uint(size=16) picSize[2];
	uint(size=6)  xIdx;
	uint(size=6)  yIdx;
	uint(size=1)  numPocRef;
	uint(size=3)  nbList;
	uint(size=3)  sizeOfList;
	bool          isNewPicture;

	/** Getting informations from new slice/picture. Updating these informations **/
	isNewSlice: action IsReadOrNewSlc:[isRead], PicSize:[picWidthInPix,
		picHeightInPix], Poc:[poc] ==>
	guard
		/* Fix me : We should know when there is a new picture */
		isRead = 0 and currIdx != -1 and poc = pocRef[currIdx]
	do
		isNewPicture := false;
		nbList := 0;
		isStillRef := [false: for int i in 0 .. DPB_SIZE - 1];
	end
uint pictNum;
	isNewPict: action IsReadOrNewSlc:[isRead], PicSize:[picWidthInPix,
		picHeightInPix], Poc:[poc] ==>
	guard
		isRead = 0 and not(currIdx != -1 and poc = pocRef[currIdx])
	do
//		println("Dpb ; Pict num "+pictNum);
		pictNum := pictNum + 1;
		picSizeInCu[0] := (picWidthInPix+15)/16;
		picSizeInCu[1] := (picHeightInPix+15)/16;
		picSize[0] := picWidthInPix;
		picSize[1] := picHeightInPix;
		xIdx := 0;
		yIdx := 0;
		lastIdx := currIdx;
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(not isRef[i]) then
				currIdx := i;
			end
		end
		isRef[currIdx]  := true;
		pocRef[currIdx] := poc;
		isNewPicture    := true;
		nbList          := 0;
		isStillRef := [false: for int i in 0 .. DPB_SIZE - 1];
	end

	/** We get all the references from the 5 lists of the Rps
	 * (StCurrentBefore, StCurrentAfter, StFollowing, LtCurrent, LtFollowing)
	 **/
	getRps.launch: action RpsPoc:[numPic]==>
	guard
		nbList < 5
	do
		sizeOfList := numPic;
	end

	getRps.done: action ==>
	guard
		nbList = 5
	do
		/* We remove from the Dpb all the pictures who are not in the Rps of
		 * the current picture
		 */
		foreach int i in 0 .. DPB_SIZE - 1
		do
			isRef[i] := isRef[i] and isStillRef[i];
		end
		isRef[currIdx] := true;
	end

	/** For each we check the index of the reference poc
	 * (in order to not remove them from the Dpb).
	 **/
	getRpsPoc.launch: action RpsPoc:[pocVal] ==>
	guard
		sizeOfList > 0
	do
		sizeOfList := sizeOfList - 1;
		foreach int i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = pocVal)then
				isStillRef[i] := true;
			end
		end
	end

	getRpsPoc.done: action ==>
	guard
		sizeOfList = 0
	do
		nbList := nbList + 1;
	end

	/* We get the whole Cu decoded (luma and chroma components) and we save
	 * it in the Dpb (pictureBuffer).
	 */
	getCuPix.launch: action Sample:[samp] repeat 8*8==>
	guard
		yIdx < picSizeInCu[1]
	var
		uint(size=16) xPixIdx,
		uint(size=16) yPixIdx
	do
		xPixIdx := (BORDER_SIZE + xIdx*16)>>1;
		yPixIdx := (BORDER_SIZE + yIdx*16)>>1;
		
		foreach uint y in 0 .. 7
		do
			foreach uint x in 0 .. 7
			do
				pictureBuffer[lastIdx][yPixIdx+y][xPixIdx+x] := samp[x+y*8];
			end
		end

		xIdx := xIdx + 1;
		if(xIdx = picSizeInCu[0]) then
			xIdx := 0;
			yIdx := yIdx + 1;
		end
	end

	/* When we get the picture we add the border (in order to be able to give a
	 * reference pixels which can be outside of the picture).
	 */
	getCuPix.done: action ==>
	guard
		yIdx = picSizeInCu[1]
	var
		uint xSize := picSize[0],
		uint ySize := picSize[1]
	do
	/* Fill in chroma border */
		foreach uint x in BORDER_SIZE/2 .. xSize/2-1 + BORDER_SIZE/2
		do
			foreach uint y in 0 .. BORDER_SIZE/2 - 1
			do
				pictureBuffer[lastIdx][y][x] :=
					pictureBuffer[lastIdx][BORDER_SIZE/2][x];
				pictureBuffer[lastIdx][y+ySize/2+BORDER_SIZE/2][x] :=
					pictureBuffer[lastIdx][ySize/2+BORDER_SIZE/2-1][x];
			end
		end
		foreach uint y in 0 .. ySize/2 + BORDER_SIZE - 1
		do
			foreach uint x in 0 .. BORDER_SIZE/2 - 1
			do
				pictureBuffer[lastIdx][y][x] :=
					pictureBuffer[lastIdx][y][BORDER_SIZE/2];
				pictureBuffer[lastIdx][y][x+xSize/2+BORDER_SIZE/2] :=
					pictureBuffer[lastIdx][y][xSize/2+BORDER_SIZE/2-1];
			end
		end
		fillBorder_chroma_cal(
		  pictureBuffer,
		  lastIdx,
		  xSize / 2,
		  ySize / 2,
		  BORDER_SIZE / 2);
	end

	getReadAddr: action IsReadOrNewSlc:[isRead], IsBiPredOrLx:[isBiPredOrLx] ==>
	guard
		isRead = 1
	do
		numPocRef :=
			if(isBiPredOrLx = BI_PRED) then
				2
			else
				1
			end;
	end

	/* We get all the informations needed for sending the reference Cu. We save
	 * these samples in refCu, it will be sent with the actions sendCu.isYxY
	 */
	getMvInfo.launch.is64: action PocRef:[poc], PredCuSize:[predCuSize],
		Mv:[mvX, mvY] ==> RefSample:[refCu] repeat(32+3)*(32+3)
	guard
		numPocRef != 0 && predCuSize = 64
	var
		uint(size=3) idx := 0,
		int (size=32) xOffset,
		int (size=32) yOffset,
		uint(size=7)  sideMax = 64,
		uint(size=8)  refCu[(32+3)*(32+3)]
	do
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = poc and isRef[i]) then
				idx := i;
			end
		end

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xOffset := (mvX >> 3);
		xOffset := min(xOffset, picSize[0]/2+1);
		xOffset := max(xOffset, -(4 +1 -1) -(sideMax/2-1));
		xOffset := xOffset +BORDER_SIZE/2-1;
		yOffset := (mvY >> 3);
		yOffset := min(yOffset, picSize[1]/2+1);
		yOffset := max(yOffset, -(4 +1 -1) -(sideMax/2-1));
		yOffset := yOffset +BORDER_SIZE/2-1;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 3 to sideMax
		 */
		
		foreach int y in 0 .. sideMax/2+3 -1
		do
			foreach int x in 0 .. sideMax/2+3 -1
			do
				refCu[x+y*(sideMax/2+3)] := pictureBuffer[idx][y+yOffset][x+xOffset];
			end
		end
		numPocRef := numPocRef - 1;
	end
	
	getMvInfo.launch.is32: action PocRef:[poc], PredCuSize:[predCuSize],
		Mv:[mvX, mvY] ==> RefSample:[refCu] repeat(16+3)*(16+3)
	guard
		numPocRef != 0 && predCuSize = 32
	var
		uint(size=3) idx := 0,
		int (size=32) xOffset,
		int (size=32) yOffset,
		uint(size=7)  sideMax = 32,
		uint(size=8)  refCu[(16+3)*(16+3)]
	do
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = poc and isRef[i]) then
				idx := i;
			end
		end

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xOffset := (mvX >> 3);
		xOffset := min(xOffset, picSize[0]/2+1);
		xOffset := max(xOffset, -(4 +1 -1) -(sideMax/2-1));
		xOffset := xOffset +BORDER_SIZE/2-1;
		yOffset := (mvY >> 3);
		yOffset := min(yOffset, picSize[1]/2+1);
		yOffset := max(yOffset, -(4 +1 -1) -(sideMax/2-1));
		yOffset := yOffset +BORDER_SIZE/2-1;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 3 to sideMax
		 */
		
		foreach int y in 0 .. sideMax/2+3 -1
		do
			foreach int x in 0 .. sideMax/2+3 -1
			do
				refCu[x+y*(sideMax/2+3)] := pictureBuffer[idx][y+yOffset][x+xOffset];
			end
		end
		numPocRef := numPocRef - 1;
	end
	
	getMvInfo.launch.is16: action PocRef:[poc], PredCuSize:[predCuSize],
		Mv:[mvX, mvY] ==> RefSample:[refCu] repeat(8+3)*(8+3)
	guard
		numPocRef != 0 && predCuSize = 16
	var
		uint(size=3) idx := 0,
		int (size=32) xOffset,
		int (size=32) yOffset,
		uint(size=7)  sideMax = 16,
		uint(size=8)  refCu[(8+3)*(8+3)]
	do
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = poc and isRef[i]) then
				idx := i;
			end
		end

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xOffset := (mvX >> 3);
		xOffset := min(xOffset, picSize[0]/2+1);
		xOffset := max(xOffset, -(4 +1 -1) -(sideMax/2-1));
		xOffset := xOffset +BORDER_SIZE/2-1;
		yOffset := (mvY >> 3);
		yOffset := min(yOffset, picSize[1]/2+1);
		yOffset := max(yOffset, -(4 +1 -1) -(sideMax/2-1));
		yOffset := yOffset +BORDER_SIZE/2-1;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 3 to sideMax
		 */
		
		foreach int y in 0 .. sideMax/2+3 -1
		do
			foreach int x in 0 .. sideMax/2+3 -1
			do
				refCu[x+y*(sideMax/2+3)] := pictureBuffer[idx][y+yOffset][x+xOffset];
			end
		end
		numPocRef := numPocRef - 1;
	end
	
	getMvInfo.launch.is8: action PocRef:[poc], PredCuSize:[predCuSize],
		Mv:[mvX, mvY] ==> RefSample:[refCu] repeat(4+3)*(4+3)
	guard
		numPocRef != 0 && predCuSize = 8
	var
		uint(size=3) idx := 0,
		int (size=32) xOffset,
		int (size=32) yOffset,
		uint(size=7)  sideMax = 8,
		uint(size=8)  refCu[(4+3)*(4+3)]
	do
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = poc and isRef[i]) then
				idx := i;
			end
		end

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xOffset := (mvX >> 3);
		xOffset := min(xOffset, picSize[0]/2+1);
		xOffset := max(xOffset, -(4 +1 -1) -(sideMax/2-1));
		xOffset := xOffset +BORDER_SIZE/2-1;
		yOffset := (mvY >> 3);
		yOffset := min(yOffset, picSize[1]/2+1);
		yOffset := max(yOffset, -(4 +1 -1) -(sideMax/2-1));
		yOffset := yOffset +BORDER_SIZE/2-1;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 3 to sideMax
		 */
		
		foreach int y in 0 .. sideMax/2+3 -1
		do
			foreach int x in 0 .. sideMax/2+3 -1
			do
				refCu[x+y*(sideMax/2+3)] := pictureBuffer[idx][y+yOffset][x+xOffset];
			end
		end
		numPocRef := numPocRef - 1;
	end
	
	getMvInfo.launch.is4: action PocRef:[poc], PredCuSize:[predCuSize],
		Mv:[mvX, mvY] ==> RefSample:[refCu] repeat(2+3)*(2+3)
	guard
		numPocRef != 0 && predCuSize = 4
	var
		uint(size=3) idx := 0,
		int (size=32) xOffset,
		int (size=32) yOffset,
		uint(size=7)  sideMax = 4,
		uint(size=8)  refCu[(2+3)*(2+3)]
	do
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = poc and isRef[i]) then
				idx := i;
			end
		end

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xOffset := (mvX >> 3);
		xOffset := min(xOffset, picSize[0]/2+1);
		xOffset := max(xOffset, -(4 +1 -1) -(sideMax/2-1));
		xOffset := xOffset +BORDER_SIZE/2-1;
		yOffset := (mvY >> 3);
		yOffset := min(yOffset, picSize[1]/2+1);
		yOffset := max(yOffset, -(4 +1 -1) -(sideMax/2-1));
		yOffset := yOffset +BORDER_SIZE/2-1;
		/* We also need the 2 next pixels for the prediction, that's why we
		 * add 3 to sideMax
		 */
		
		foreach int y in 0 .. sideMax/2+3 -1
		do
			foreach int x in 0 .. sideMax/2+3 -1
			do
				refCu[x+y*(sideMax/2+3)] := pictureBuffer[idx][y+yOffset][x+xOffset];
			end
		end
		numPocRef := numPocRef - 1;
	end
	
	
	getMvInfo.done: action ==>
	guard
		numPocRef = 0
	end

	checkIsNewPict.isTrue: action ==>
	guard
		isNewPicture
	end

	checkIsNewPict.isFalse: action ==>
	guard
		not isNewPicture
	end

	schedule fsm IsFirstNewPic:
		IsFirstNewPic (isNewPict )--> GetFirstRps;

		GetFirstRps (getRps.launch )--> GetRpsFirstPoc;
		GetFirstRps (getRps.done   )--> IsNewSlice;

		GetRpsFirstPoc (getRpsPoc.launch )--> GetRpsFirstPoc;
		GetRpsFirstPoc (getRpsPoc.done   )--> GetFirstRps;

		IsNewSlice (isNewPict   )--> GetRps;
		IsNewSlice (isNewSlice  )--> GetRps;
		IsNewSlice (getReadAddr )--> GetMvInfo;

		GetRps (getRps.launch )--> GetRpsPoc;
		GetRps (getRps.done   )--> CheckIsNewPict;

		GetRpsPoc (getRpsPoc.launch )--> GetRpsPoc;
		GetRpsPoc (getRpsPoc.done   )--> GetRps;

		CheckIsNewPict (checkIsNewPict.isTrue  )--> GetCuPix;
		CheckIsNewPict (checkIsNewPict.isFalse )--> IsNewSlice;

		GetCuPix (getCuPix.launch )--> GetCuPix;
		GetCuPix (getCuPix.done   )--> IsNewSlice;

		GetMvInfo (getMvInfo.launch )--> GetMvInfo;
		GetMvInfo (getMvInfo.done   )--> IsNewSlice;
	end
end
