package org.ietr.mpegh.part2.main.tiles;
import devel.org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;

actor tilesMerger2Tiles () 	uint (size= 8) IN1, 
						uint (size= 8) IN2, 
						uint(size=16) TilesCoord
  						==> 
  						uint (size= 8) OUT:

uint counter0 := 0;
uint counter1 := 0;


uint countOut0 := 0;
uint countOut1 := 0;


int(size=16) iLoop;
uint(size=16) numTiles;

uint (size=8) tab0[384*480*3/2];
uint (size=8) tab1[448*480*3/2];

uint tileSize[MAX_NUM_TILES];

	readWrite.Rtile0: action IN1:[data] repeat 384 ==> 
	guard
		counter0 < tileSize[0]*3/2
	do
		foreach int i in 0 .. 383 do
			tab0[counter0 + i] := data[i];
		end
		counter0 := counter0 + 384;
	end
	
	readWrite.Rtile1: action IN2:[data] repeat 384 ==> 
	guard
		counter1 < tileSize[1]*3/2
	do
		foreach int i in 0 .. 383 do
			tab1[counter1 + i] := data[i];
		end
		counter1 := counter1 + 384;
	end
	
	getNumTiles: action TilesCoord:[valNumTiles] ==>
	do
		numTiles := valNumTiles;
		iLoop := 0;
	end

	getTilesCoord.launch: action TilesCoord:[tilesCoord] repeat 4 ==>
	var
		uint tileWidth,
		uint tileHeight
	do
		tileWidth  := tilesCoord[2] - tilesCoord[0];
		tileHeight := tilesCoord[3] - tilesCoord[1];
		tileSize[iLoop] := tileWidth * tileHeight;
		iLoop := iLoop + 1;
	end

	getTilesCoord.done: action ==>
	guard
		iLoop = numTiles
	do
		iLoop := 0;
	end
	
	
	readWrite.WTile0: action ==> OUT:[[tab0[cnt + i]: for int i in 0 .. 383]] repeat 384
	guard
		counter0 > countOut0
	var
		uint cnt
	do
		cnt := countOut0;
		countOut0 := countOut0 + 384;
	end
	
	readWrite.WTile1: action ==> OUT:[[tab1[cnt + i]: for int i in 0 .. 383]] repeat 384
	guard
		counter1 > countOut1,
		countOut0 = tileSize[0]*3/2
	var
		uint cnt
	do
		cnt := countOut1;
		countOut1 := countOut1 + 384;
	end
	
	endTiles: action ==>
	guard
		countOut1 = tileSize[1]*3/2
	do
		countOut0 := 0;
		countOut1 := 0;
		
		counter0 := 0;
		counter1 := 0;
		
	end
	
	
	schedule fsm GetNumTiles:
		GetNumTiles   	(getNumTiles          )	--> GetTilesCoord;
		GetTilesCoord 	(getTilesCoord.launch )	--> GetTilesCoord;
		GetTilesCoord 	(getTilesCoord.done   )	--> readTiles;
		readTiles		(readWrite)				--> readTiles;
		readTiles		(endTiles)				--> GetNumTiles;
	end
	
	priority
		getTilesCoord.done > getTilesCoord.launch;
		endTiles  > readWrite;
	end
end